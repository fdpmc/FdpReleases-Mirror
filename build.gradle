version = "v1"
group = "org.unlegitmc.fdp"

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()

        maven { url 'https://maven.minecraftforge.net/' }
        maven { url 'https://plugins.gradle.org/m2' }
        maven { url 'https://repo.spongepowered.org/maven' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: '1.3.72'
        classpath group: 'com.github.SkidderMC', name: 'ForgeGradle', version: 'f2c5bb338e'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.6-SNAPSHOT'
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '4.0.4'
    }
}

// Languages
apply plugin: 'java'
apply plugin: 'kotlin'

// IDE
apply plugin: 'idea'

// Minecraft
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

// Jar packaging
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

archivesBaseName = 'FDPClient'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'
    makeObfSourceJar = true
}

repositories {
    mavenLocal()
    mavenCentral()

    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.spongepowered.org/maven/' }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.10-SNAPSHOT') {
        exclude module: 'launchwrapper'
    }

    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    implementation group: 'com.github.UnlegitMC', name: 'FuckPCL', version: '1.1'

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.3.72'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk7', version: '1.3.72'

    implementation group: 'com.labymedia', name: 'ultralight-java-base', version: '0.4.6'
    implementation group: 'com.labymedia', name: 'ultralight-java-databind', version: '0.4.6'
    implementation project(":ui")
    // GPUDriver need the GLFW windowId to run, but LWJGL2 dont support GLFW
//    implementation group: 'com.labymedia', name: 'ultralight-java-gpu', version: '0.4.6'

    compile fileTree(include: ['*.jar'], dir: 'libs')
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

mixin {
    add sourceSets.main, 'liquidbounce.mixins.refmap.json'
}

jar {
    manifest.attributes(
            'MixinConfigs': 'liquidbounce.forge.mixins.json',
            'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'FMLCorePluginContainsFMLMod': 'net.ccbluex.liquidbounce.injection.forge.MixinLoader',
            'FMLCorePlugin': 'net.ccbluex.liquidbounce.injection.forge.TransformerLoader',
            'FMLAT': 'liquidbounce_at.cfg',
            'Manifest-Version': 1.0
    )
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('org.slf4j:slf4j-api'))
        include(dependency('log4j:log4j:1.2.17'))
        include(dependency('org.scijava:native-lib-loader'))
        include(dependency('com.kohlschutter.junixsocket:junixsocket-common'))
        include(dependency('com.kohlschutter.junixsocket:junixsocket-native-common'))

        include(dependency('org.reflections:reflections'))
        // depend of reflections
        include(dependency('org.javassist:javassist'))

        include(dependency('com.github.UnlegitMC:FuckPCL'))

        // kotlin
        include(dependency('org.jetbrains.kotlin:kotlin-stdlib'))
        include(dependency('org.jetbrains.kotlin:kotlin-stdlib-jdk7'))
        include(dependency('org.jetbrains:annotations'))

        // ultralight
        include(dependency('com.labymedia:ultralight-java-base'))
        include(dependency('com.labymedia:ultralight-java-databind'))
        include(project(":ui"))
    }

    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    exclude 'LICENSE'
    exclude 'NOTICE'

    // Ultralight native files are going to be downloaded from CCBlueX's cloud
    exclude "native-binaries/*"

    classifier = ''
}

reobf { shadowJar { mappingType = 'SEARGE' } }
tasks.reobfShadowJar.mustRunAfter shadowJar

configurations {
    external
    compile.extendsFrom external
}

build.dependsOn shadowJar